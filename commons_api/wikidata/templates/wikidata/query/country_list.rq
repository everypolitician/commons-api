# Adapted from https://github.com/everypolitician/legislative-explorer/blob/master/lib/query/country_list.rb

SELECT DISTINCT ?item ?itemLabel ?itemCode WHERE {
  ?item p:P31 ?instanceOfStatement .
  ?instanceOfStatement ps:P31/wdt:P279* wd:Q6256 .
  MINUS { ?instanceOfStatement pq:P582 ?end }  # no longer a country
  ?item p:P463 ?memberOfStatement .
  ?memberOfStatement ps:P463 wd:Q1065 .
  MINUS { ?memberOfStatement pq:P582 ?end }    # no longer a member of the UN
  MINUS { ?item wdt:P1552 wd:Q47185282 }       # not free
  OPTIONAL { ?item wdt:P297 ?itemCode }        # ISO 3166-1 code
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
} ORDER BY ?itemLabel
